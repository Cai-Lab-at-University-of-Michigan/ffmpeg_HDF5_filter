apply plugin: 'java'

group = 'com.cailab'
version = '1.8.6'

description = """Combined HDF5-FFMPEG Compression and Viewer"""

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

repositories {
    mavenCentral()
    maven { url "https://artifacts.psi.ch/artifactory/libs-releases" }
    maven { url "https://maven.imagej.net/content/repositories/public/" }
}

// define a provided scope
configurations {
    provided
    implementation.extendsFrom provided
}

dependencies {
    // Commons-IO from Maven Central - updated to latest stable version
    implementation group: 'commons-io', name: 'commons-io', version: '2.16.1'
    
    // SLF4J - updated to latest stable version
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.13'
    
    // Add SLF4J implementation for runtime logging
    runtimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.13'
    
    // ImageJ dependency - updated to latest version
    provided group: 'net.imagej', name: 'ij', version: '1.54p'
}

// Create combined fat JAR
task packJar(type: Jar) {
    archiveBaseName = 'HDF5_ffmpeg'
    
    // Include compiled classes and resources
    from { (configurations.runtimeClasspath - configurations.provided).collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    
    // Exclude unnecessary files from dependencies
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}